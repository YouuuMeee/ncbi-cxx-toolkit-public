# $Id$

NCBI_begin_lib(macro)
    NCBI_dataspecs(macro.asn)
    NCBI_uses_toolkit_libraries(seq)
    NCBI_project_watchers(stakhovv kans)

    if (NOT CMAKE_CROSSCOMPILING AND NCBI_FSM_COMPILER_ENABLED)
        NCBI_custom_target_dependencies(macro_fsm_files)
    endif()

NCBI_end_lib()

NCBI_begin_custom_target(macro_fsm_files)
    NCBI_custom_target_definition(define_macro_fsm_files_target)
    NCBI_custom_target_dependencies(multipattern)
NCBI_end_custom_target()

function(define_macro_fsm_files_target)
    set(_prt_modules
        weasel.txt
    )

    define_multipatern_inc_target(macro_fsm_files multipattern "${_prt_modules}" "-A")
endfunction()

function(define_multipatern_inc_target up_target compiler modules more_args)

    set (_compiler ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${compiler}${CMAKE_EXECUTABLE_SUFFIX})

    set(_source_dir ${NCBI_CURRENT_SOURCE_DIR})
    set(_target_dir ${NCBI_CURRENT_SOURCE_DIR})

    #set(_make_fsm_file ${NCBI_CURRENT_SOURCE_DIR}/../../objects/macro/make_fsm_file.py)
    set(_make_fsm_file ${NCBI_SRC_ROOT}/objects/macro/make_fsm_file.py)

    add_custom_target(${up_target}
        COMMENT "Generate FSM files"
        SOURCES ${_make_fsm_file}
        )

    foreach(_module ${modules})

        # remove suffix
        string(REGEX REPLACE "\\..*" "" _tmp_module_name "${_module}")

        set(_prt_file ${_module})
        set(_inc_file ${_tmp_module_name}.inc)
        set(_target fsm_${_tmp_module_name}_inc)

        add_custom_command(
            OUTPUT ${_target_dir}/${_inc_file}
            COMMAND python3 ${_make_fsm_file} ${_inc_file} ${_compiler} -i ${_prt_file} ${more_args}
            WORKING_DIRECTORY ${_source_dir}
            COMMENT "Running make_fsm_file.py for ${_prt_file}"
            DEPENDS ${_source_dir}/${_prt_file} ${_make_fsm_file} ${_compiler}
            )

        add_custom_target(
            ${_target}
            DEPENDS ${_target_dir}/${_inc_file}
        )
        add_dependencies(${up_target} ${_target})
    endforeach()

    unset(_inc_file)
    unset(_prt_file)
    unset(_target)
    unset(_target_dir)
    unset(_source_dir)
    unset(_compiler)
endfunction()
