# $Id$

set(_discrepancy_modules
    biosource_tests
    discrepancy_case
    division_code_conflicts
    feature_per_bioseq
    feature_tests
    gene_names
    overlapping_features
    pub_tests
    rna_names
    seqdesc_per_bioseq
    sequence_tests
    sesame_street
    #spell_check #disabled till better times...
    suspect_product_names
    transl_note
    transl_too_long
)

NCBI_begin_lib(xdiscrepancy)
    NCBI_sources(
        # core modules
        discrepancy_core discrepancy_context discrepancy_stream report_object utils text_output
        fsm_statics
        #individual tests
        ${_discrepancy_modules}
    )
    NCBI_uses_toolkit_libraries(xcompress macro xcleanup xobjedit)
    NCBI_project_watchers(gotvyans)

    if (NOT CMAKE_CROSSCOMPILING AND NCBI_FSM_COMPILER_ENABLED)
        NCBI_custom_target_dependencies(discrepancy_fsm_files)
    endif()
    if(ON) # disable until RW-1852
        NCBI_custom_target_dependencies(discrepancy_testnames)
    endif()


NCBI_end_lib()

function(define_discrepancy_fsm_files_target)
    set(_prt_modules
        product_rules.prt
        organelle_products.prt
        FLATFILE_FIND.txt
    )

    define_multipatern_inc_target(discrepancy_fsm_files prt2fsm "${_prt_modules}" "")
endfunction()


NCBI_begin_custom_target(discrepancy_fsm_files)
    NCBI_custom_target_definition(define_discrepancy_fsm_files_target)
    NCBI_custom_target_dependencies(prt2fsm)
NCBI_end_custom_target()

NCBI_begin_custom_target(discrepancy_testnames)
    NCBI_custom_target_definition(define_discrepancy_testnames_target)
NCBI_end_custom_target()


function(define_discrepancy_testnames_target)
    set(_target_dir ${NCBI_INC_ROOT}/misc/discrepancy)

    set(_source_dir ${NCBI_CURRENT_SOURCE_DIR})
    set(_cpp_files)

    foreach(_module ${_discrepancy_modules})
       list(APPEND _cpp_files ${_module}.cpp)
    endforeach()

    add_custom_command(
        OUTPUT ${_target_dir}/testnames.inc
        COMMAND python3 make_test_names.py ${_target_dir}/testnames.inc ${_cpp_files}
        COMMENT "Running make_test_names.py for ${discrepancy_modules} modules"
        WORKING_DIRECTORY ${_source_dir}
        DEPENDS ${_cpp_files} ${_source_dir}/make_test_names.py
        )

    add_custom_target(
        discrepancy_testnames
        DEPENDS ${_target_dir}/testnames.inc
    )
    add_dependencies(xdiscrepancy discrepancy_testnames)
    unset(_cpp_files)
    unset(_target_dir)
    unset(_source_dir)
endfunction()
